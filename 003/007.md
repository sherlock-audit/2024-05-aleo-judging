Plain Aegean Ram

Medium

# Bypass the `split` fee using `transfer_private`

## Summary

`transfer_private` can be used to `split` a credit record without paying a `split` fee.

## Vulnerability Detail

In Aleo, we can choose to split a credit record (which are similar to UTXO)

https://github.com/sherlock-audit/2024-05-aleo/blob/main/snarkVM/synthesizer/program/src/resources/credits.aleo#L947-L972
```aleo
/ The `split` function splits a record into two records. The given input amount will be stored in the first record,
// and the remaining amount will be stored in the second record, with the fee deducted from the remaining amount.
// If the caller executes a transaction that contains only a call to this function, then the transaction does not
// require a fee, unless the caller wishes to provide an additional fee. Transactions that contain multiple transitions
// (that include one or more calls to this function) will require a fee as per standard consensus rules.
function split:
    // Input the record.
    input r0 as credits.record;
    // Input the amount to split.
    input r1 as u64.private;
    // Checks the given record has a sufficient amount to split.
    // This `sub` operation is safe, and the proof will fail
    // if an underflow occurs.
    sub r0.microcredits r1 into r2;
    // Checks the given record has a sufficient fee to remove.
    // This `sub` operation is safe, and the proof will fail
    // if an underflow occurs.
    sub r2 10_000u64 into r3;
    // Construct the first record.
    cast r0.owner r1 into r4 as credits.record;
    // Construct the second record.
    cast r0.owner r3 into r5 as credits.record;
    // Output the first record.
    output r4 as credits.record;
    // Output the second record.
    output r5 as credits.record;
```

For instance, let's suppose we have a record with a credit of 1000 and we want to split into 2 records of 500 credits each with the same owner.

By calling `split` on the record with `r1 = 500 credits` we can split the record into two records with the initial owner, a record containing 500 credits and a record containing 500 credits - 10000 microcredits. Here we can see we incur a fee of 10000 microcredits.

But such a fee can be bypassed because `transfer_private` can be made to do something functionally similar:

https://github.com/sherlock-audit/2024-05-aleo/blob/main/snarkVM/synthesizer/program/src/resources/credits.aleo#L829-L850
```aleo
// The `transfer_private` function sends the specified amount
// from the sender's record to the receiver in a record.
function transfer_private:
    // Input the sender's record.
    input r0 as credits.record;
    // Input the receiver.
    input r1 as address.private;
    // Input the amount.
    input r2 as u64.private;
    // Checks the given record has a sufficient amount.
    // This `sub` operation is safe, and the proof will fail
    // if an underflow occurs. The destination register `r3` holds
    // the change amount to be returned to the sender.
    sub r0.microcredits r2 into r3;
    // Construct a record for the specified receiver.
    cast r1 r2 into r4 as credits.record;
    // Construct a record with the change amount for the sender.
    cast r0.owner r3 into r5 as credits.record;
    // Output the receiver's record.
    output r4 as credits.record;
    // Output the sender's change record.
    output r5 as credits.record;
```

So going back to our example we can call `transfer_private` with `r1 = r0.owner` and `r2 = 500 credits`. This will allow us to generate 2 records with the same initial owner, each with 500 credits without paying the 10_000 microcredit split fee.

## Impact

Bypass the `split` fee.

## Code Snippet

https://github.com/sherlock-audit/2024-05-aleo/blob/main/snarkVM/synthesizer/program/src/resources/credits.aleo#L829-L850

https://github.com/sherlock-audit/2024-05-aleo/blob/main/snarkVM/synthesizer/program/src/resources/credits.aleo#L947-L972

## Tool used

Manual Review

## Recommendation

I think you should also charge the fee in `transfer_private`